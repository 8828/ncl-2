#!/bin/csh -f -v
#
#      $Id: ymkmf,v 1.3 1993-12-09 22:12:23 boote Exp $
#
#########################################################################
#									#
#			   Copyright (C)  1993				#
#	     University Corporation for Atmospheric Research		#
#			   All Rights Reserved				#
#									#
#########################################################################
#
#	File:		ymkmf
#
#	Author:		Jeff W. Boote
#			National Center for Atmospheric Research
#			PO 3000, Boulder, Colorado
#
#	Date:		Tue Nov 2 09:22:31 MST 1993
#
#	Description:	This is a wrapper function for ymake so that
#			it is easier to create a Makefile in any sub-directory
#			of the distribution. This script parses the cwd
#			and searches up the tree until it finds the name
#			"ncarg" or "$topdirname".  It considers that to be
#			the top of your current distribution.  If the script
#			is passed "$topdirname" it just passes TOPDIR and
#			CURDIR to ymake, and uses ymake from the installed
#			area.  If it is called without any args, then it
#			assumes ymake is in the "config" directory that is
#			a direct sub-dir of "ncarg".
#
#	Usage:		ymkmf [topdirname]
#
#	Environment:	csh script
#
#	Files:		ymake
#
#
#	Options:	topdirname

set	topdir	= .
set	thisdir = `pwd`

set	defines

while ($#argv)

	switch ("$argv[1]")

	case	-local
		set defines = ($defines -local)

		breaksw

	default:
		# make sure "topname" is last arg
		if ($#argv != 1) then
			echo "$0 : Usage `basename $0` [-local] topname"
			exit 1
		endif

		set	topname = $argv[1]
		@ useinstalled = 1

		breaksw
	endsw

	shift
end

if (! $?topname) then
	set	topname = ncarg
	@ useinstalled = 0
endif

if ("$topname" == ".") then
	set	topdir = .
	set	curdir = .
	goto shortcut
endif

echo $thisdir | fgrep $topname >& /dev/null
if ($status != 0) then
	echo "$0 : Unable to find TopDir $topname in $thisdir"
	exit 1
endif

echo $topname | fgrep / >& /dev/null
if ($status != 1) then
	echo "$0 : Bad TopDir name $topname"
	exit 1
endif

set	base = `basename $thisdir`
set	ndir = `dirname $thisdir`

while ("$base" != "$topname")
	if($?curdir) then
		set curdir = "$base"/"$curdir"
	else
		set curdir = "$base"
	endif

	set topdir = ../"$topdir"

	set base = `basename $ndir`
	set ndir = `dirname ${ndir}`
end

if($?curdir) then
	set curdir = ./"$curdir"
else
	set curdir = .
endif

shortcut:

set defines = ($defines -Curdir "$curdir" -Topdir "$topdir")

if ($useinstalled == 1) then
	exec ymake $defines
else
	if ( ! -x "$topdir"/config/ymake-filter) then
		echo "Making ymake from Makefile.ini in "$topdir"/config first"
		(cd "$topdir"/config; make -f Makefile.ini clean all)
		echo "continuing in `pwd`"
	endif
	exec "$topdir"/config/ymake -config "$topdir"/config $defines
endif
