C
C	$Id: pwrtm.f,v 1.3 2000-08-22 15:10:25 haley Exp $
C                                                                      
C                Copyright (C)  2000
C        University Corporation for Atmospheric Research
C                All Rights Reserved
C
C This file is free software; you can redistribute it and/or modify
C it under the terms of the GNU General Public License as published
C by the Free Software Foundation; either version 2 of the License, or
C (at your option) any later version.
C
C This software is distributed in the hope that it will be useful, but
C WITHOUT ANY WARRANTY; without even the implied warranty of
C MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
C General Public License for more details.
C
C You should have received a copy of the GNU General Public License
C along with this software; if not, write to the Free Software
C Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307
C USA.
C
      SUBROUTINE PWRTM (X,Y,CHARS,JSIZE,JOR,JCNTR)
C
C
C PWRTM CAN BE CALLED TO DRAW CHARACTERS AND MARK THE REGIONS WHERE THE
C CHARACTERS HAVE BEEN DRAWN IN THE MODEL PICTURE.
C
C THE PARAMETERS TO PWRTM ARE EXACTLY THE SAME AS THE PARAMETERS TO
C THE UTILITY SUPPORT ROUTINE WTSTR.
C
C THE COORDINATES X AND Y ARE IN THE USER'S RESOLUTION, SINCE PWRTM
C IS A USER ENTRY POINT
C
      CHARACTER*(*) CHARS
C
C ISKIP IS INITIALIZED AND DESCRIBED IN BLOCKDATA DASHBD.
C
      COMMON /DSUP1/ ISKIP
C
      COMMON/INTPR/IPAU,FPART,TENSN,NP,SMALL,L1,ADDLR,ADDTB,MLLINE,
     1    ICLOSE
      SAVE
C
C
C A CONSTANT USED TO COMPUTE THE NUMBER OF LINES TO BE MARKED.
C SQTDTH = SQRT(2)/3
C
      DATA SQTDTH /.47133/
C
C COPY INTO LOCAL VARIABLES
C
      ICNTR = JCNTR
      ISIZE = JSIZE
      IOR = JOR
C
C RETRIEVE THE USER'S RESOLUTION.
C
         CALL GETUSV('XF',LXSAVE)
         CALL GETUSV('YF',LYSAVE)
C
C DRAW THE CHARACTER STRING
C
      CALL WTSTR (X,Y,CHARS,ISIZE,IOR,ICNTR)
C
C RETURN IF STRING IS OF LENGTH 0
C
      IC = LEN(CHARS)
      IF (IC .EQ. 0) RETURN
C
C TRANSFORM COORDINATES INTO RESOLUTION 15.
C
      CALL FL2INT (X,Y,MXS,MYS)
C
C ADJUST ADDLR AND ADDTB TO 15 BIT RESOLUTION.
C
      IFIFTE = ISHIFT (1,15-10)
      ATBAD = ADDTB*FLOAT(IFIFTE)
      ALRAD = ADDLR*FLOAT(IFIFTE)
C
C COMPUTE THE SIZE OF A CHARACTER IN THE STRING DRAWN
C
      FISIZE = ISIZE*ISHIFT(1,15-LXSAVE)
      IF (ISIZE .GT. 3) GO TO 30
      FISIZE = 256. + FLOAT(ISIZE)*128.
      IF (ISIZE .EQ. 3) FISIZE = 768.
   30 CONTINUE
      LNT = IFIX(FISIZE*FLOAT(IC)+2.*ALRAD+.5)
C
C COMPUTE THE ANGLE IN WHICH THE STRING WAS DRAWN
C
      ANGLE = (FLOAT(IOR)*2.*3.1415)/360.
C
      SA = SIN(ANGLE)
      CA = COS(ANGLE)
C
C CORRECT FOR CENTERING OPTION.
C
      IF (ICNTR .EQ. 0) SHIFT = .5*FLOAT(LNT)
      IF (ICNTR .EQ. -1) SHIFT = ALRAD
      IF (ICNTR .EQ. 1) SHIFT = FLOAT(LNT)-.5*FISIZE-ALRAD
C
C ADD FREE SPACE AT TOP AND BOTTOM
C
      FISIZE = FISIZE + 2.*ATBAD
C
C FIND THE LEFT AND THE RIGHT END OF THE AREA TO BE MARKED.
C
      MXS1 = FLOAT(MXS)-SHIFT*CA+.5*FISIZE*SA
      MYS1 = FLOAT(MYS)-SHIFT*SA-.5*FISIZE*CA
      MXS2 = FLOAT(MXS)+(FLOAT(LNT)-SHIFT)*CA+.5*FISIZE*SA
      MYS2 = FLOAT(MYS)+(FLOAT(LNT)-SHIFT)*SA-.5*FISIZE*CA
C
C COMPUTE THE NUMBER OF LINES TO BE MARKED.
C
      NLINES = FISIZE*SQTDTH
C
      R = AMAX0(NLINES-1,1)/FISIZE
C
C MARK THE AREA WHERE THE STRING WAS DRAWN
C
      DO 90 I=1,NLINES,ISKIP
         P = FLOAT(I-1)/R
         MX1 = FLOAT(MXS1)-P*SA
         MY1 = FLOAT(MYS1)+P*CA
         MX2 = FLOAT(MXS2)-P*SA
         MY2 = FLOAT(MYS2)+P*CA
         CALL MARKL (MX1,MY1,MX2,MY2)
   90 CONTINUE
C
      RETURN
C
C------REVISION HISTORY
C
C JUNE 1984          CONVERTED TO FORTRAN77 AND GKS
C
C JUNE 1988          CHANGED THE NAME OF A COMMON BLOCK TO GET RID OF A
C                    WARNING FROM SEGLDR.  (DJK)
C
C NOVEMBER 1988      REMOVED KURV1S AND KURV2S.  IMPLEMENTED CALLS TO
C                    MSKRV1 AND MSKRV2 INSTEAD.
C
C-----------------------------------------------------------------------
C
      END
