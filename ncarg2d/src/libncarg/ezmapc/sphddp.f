C
C $Id: sphddp.f,v 1.1 1999-04-19 22:10:40 kennison Exp $
C
      SUBROUTINE SPHDDP(ISPH,PARM)
C
C     SUBROUTINE TO COMPUTE SPHEROID PARAMETERS
C
C     ISPH IS THE SPHEROID CODE FROM THE FOLLOWING LIST:
C     0 = CLARKE 1866           1 = CLARKE 1880
C     2 = BESSEL                3 = NEW INTERNATIONAL 1967
C     4 = INTERNATIONAL 1909    5 = WGS 72
C     6 = EVEREST               7 = WGS 66
C     8 = GRS 1980              9 = AIRY
C    10 = MODIFIED EVEREST     11 = MODIFIED AIRY
C    12 = WGS 84               13 = SOUTHEAST ASIA
C    14 = AUSTRALIAN NATIONAL  15 = KRASSOVSKY
C    16 = HOUGH                17 = MERCURY 1960
C    18 = MODIFIED MERC 1968   19 = SPHERE OF RADIUS 6370997 M
C
C    PARM IS ARRAY OF PROJECTION PARAMETERS:
C       PARM(1) IS THE SEMI-MAJOR AXIS
C       PARM(2) IS THE ECCENTRICITY SQUARED
C
C     IF ISPH IS NEGATIVE, USER SPECIFIED PROJECTION PARAMETERS ARE TO
C     DEFINE THE RADIUS OF SPHERE OR ELLIPSOID CONSTANTS AS APPROPRIATE
C
C     IF ISPH = 0 , THE DEFAULT IS RESET TO CLARKE 1866
C
C ****                                                             *****
C
      IMPLICIT DOUBLE PRECISION (A-Z)
      INTEGER ISPH,JSPH
      DIMENSION PARM(15),AXIS(20),BXIS(20)
C
      COMMON /ELLPDP/ AZ,EZ,ESZ,E0Z,E1Z,E2Z,E3Z,E4Z
      COMMON /SPHRDP/ AZZ
      COMMON /ERRMZ0/ IERR
        INTEGER IERR
      SAVE   /ERRMZ0/
      COMMON /PRINZ0/ IPEMSG,IPELUN,IPPARM,IPPLUN
        INTEGER IPEMSG,IPELUN,IPPARM,IPPLUN
      SAVE   /PRINZ0/
      COMMON /PROJZ0/ IPRO
        INTEGER IPRO
      SAVE   /PROJZ0/
C
      DATA ZERO,ONE /0.0D0,1.0D0/
C
      DATA AXIS / 6378206.400000D0,6378249.145000D0,6377397.155000D0,
     .            6378157.500000D0,6378388.000000D0,6378135.000000D0,
     .            6377276.345200D0,6378145.000000D0,6378137.000000D0,
     .            6377563.396000D0,6377304.063000D0,6377340.189000D0,
     .            6378137.000000D0,6378155.000000D0,6378160.000000D0,
     .            6378245.000000D0,6378270.000000D0,6378166.000000D0,
     .            6378150.000000D0,6370997.000000D0 /
C
      DATA BXIS / 6356583.800000D0,6356514.869550D0,6356078.962840D0,
     .            6356772.200000D0,6356911.946130D0,6356750.519915D0,
     .            6356075.413300D0,6356759.769356D0,6356752.314140D0,
     .            6356256.910000D0,6356103.039000D0,6356034.448000D0,
     .            6356752.314245D0,6356773.320500D0,6356774.719000D0,
     .            6356863.018800D0,6356794.343479D0,6356784.283666D0,
     .            6356768.337303D0,6370997.000000D0 /
C
      IF (ISPH.GE.0) GO TO 5
C
C     INITIALIZE USER SPECIFIED SPHERE AND ELLIPSOID PARAMETERS
C
      AZZ = ZERO
C
C     FETCH FIRST TWO USER SPECIFIED PROJECTION PARAMETERS
C
      A = ABS(PARM(1))
      B = ABS(PARM(2))
      IF (A .GT. ZERO .AND. B .GT. ZERO) GO TO 13
      IF (A .GT. ZERO .AND. B .LE. ZERO) GO TO 12
      IF (A .LE. ZERO .AND. B .GT. ZERO) GO TO 11
C
C     DEFAULT NORMAL SPHERE AND CLARKE 1866 ELLIPSOID
C
      JSPH = 1
      GO TO 10
C
C     DEFAULT CLARKE 1866 ELLIPSOID
C
   11 A = AXIS(1)
      B = BXIS(1)
      GO TO 14
C
C     USER SPECIFIED RADIUS OF SPHERE
C
   12 AZZ = A
      GO TO 15
C
C     USER SPECIFIED SEMI-MAJOR AND SEMI-MINOR AXES OF ELLIPSOID
C
   13 IF (B .LE. ONE) GO TO 15
   14 ES = ONE - (B / A)**2
      GO TO 16
C
C     USER SPECIFIED SEMI-MAJOR AXIS AND ECCENTRICITY SQUARED
C
   15 ES = B
   16 IF (AZ.NE.A.OR.ESZ.NE.ES) THEN
         AZ = A
         ESZ = ES
         EZ  = SQRT(ES)
         E0Z = E0FNDP(ES)
         E1Z = E1FNDP(ES)
         E2Z = E2FNDP(ES)
         E3Z = E3FNDP(ES)
         E4Z = E4FNDP(EZ)
      END IF
      PARM(1) = A
      PARM(2) = ES
      RETURN
C
C     CHECK FOR VALID SPHEROID SELECTION
C
    5 IF (PARM(1).NE.ZERO.AND.IPRO.NE.1) RETURN
      JSPH = ABS(ISPH) + 1
      IF (JSPH.LE.20) GO TO 10
      IERR = 999
      IF (IPEMSG .EQ. 0) WRITE (IPELUN,1) ISPH
    1 FORMAT(/' ERROR SPHDDP:  SPHEROID CODE OF ',I5,' RESET TO 0')
      ISPH = 0
      JSPH = 1
C
C     RETRIEVE A AND B AXES FOR SELECTED SPHEROID
C
   10 A = AXIS(JSPH)
      B = BXIS(JSPH)
      ES = ONE - (B / A)**2
C
C     SET COMMON BLOCK PARAMETERS FOR SELECTED SPHEROID
C
      AZZ = 6370997.0D0
      IF (AZ.NE.A.OR.ESZ.NE.ES) THEN
         EZ  = SQRT(ES)
         E0Z = E0FNDP(ES)
         E1Z = E1FNDP(ES)
         E2Z = E2FNDP(ES)
         E3Z = E3FNDP(ES)
         E4Z = E4FNDP(EZ)
         AZ  = A
         ESZ = ES
         IF (ES.EQ.ZERO) AZZ=A
      END IF
C
      PARM(1) = A
      PARM(2) = ES
      RETURN
      END
