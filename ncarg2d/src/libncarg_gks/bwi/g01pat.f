C
C	$Id: g01pat.f,v 1.1.1.1 1992-04-17 22:33:58 ncargd Exp $
C
        SUBROUTINE G01PAT
C
C       PROCESS PRIMITIVE ATTRIBUTES.
C
      COMMON /GKSIN1/FCODE,CONT,IL1,IL2,ID(128),RL1,RL2,RX(128),
     - RY(128),STRL1,STRL2,RERR
      COMMON /GKSIN2/STR
      INTEGER FCODE, CONT, IL1, IL2, ID, RL1, RL2
      INTEGER STRL1, STRL2, RERR
      REAL  RX, RY
      CHARACTER*80 STR
      COMMON  /G01INS/  MCODES  ,MCONTS ,
     +                  MVDCFW  ,MCIXFW ,MDCCFW ,MIXFW  ,MINTFW ,
     +                  MDCCRG  ,MXOFF  ,MXSCAL ,MYOFF  ,MYSCAL ,
     +                  MINXVD  ,MAXXVD ,MINYVD ,MAXYVD ,
     +                  MCFRM   ,MCOPCL ,MCOPID ,MCNBYT ,
     +                  MCCBYT  ,MCFPP  ,MSLFMT ,MEFW   ,MCTCHG ,
     +                  MBCCHG
        INTEGER         MCODES  ,MCONTS
        INTEGER         MVDCFW  ,MCIXFW ,MDCCFW ,MIXFW  ,MINTFW
        INTEGER         MDCCRG  ,MXOFF  ,MXSCAL ,MYOFF  ,MYSCAL
        INTEGER         MINXVD  ,MAXXVD ,MINYVD ,MAXYVD
        INTEGER         MCFRM   ,MCOPCL ,MCOPID ,MCNBYT
        INTEGER         MCCBYT  ,MCFPP  ,MSLFMT ,MEFW   ,MCTCHG
        INTEGER         MBCCHG
      COMMON  /G01ADC/  VALCHG(37)      ,ANYASF ,AGPEND(4)      ,
     +                  IVPLIX  ,IVLTYP ,IVLWSC ,IVPLCI ,IVPMIX ,
     +                  IVMTYP  ,IVMSZS ,IVPMCI ,IVTXIX ,IVTXP  ,
     +                  IVTXAL  ,IVCHH  ,IVCHOV ,IVTXFO ,IVTXPR ,
     +                  IVCHXP  ,IVCHSP ,IVTXCI ,IVFAIX ,IVPASZ ,
     +                  IVPARF  ,IVFAIS ,IVFASI ,IVFACI ,IVASF  ,
     +                  IP2AEA(26)      ,IL2AEA(26)     ,
     +                  IALTYP  ,IALWSC ,IAPLCI ,IAMTYP ,IAMSZS ,
     +                  IAPMCI  ,IATXFP ,IACHXP ,IACHSP ,IATXCI ,
     +                  IAFAIS  ,IAFASI ,IAFACI ,
     +                  NCGASF  ,NGKASF ,MASMAP(18)
        LOGICAL         VALCHG  ,ANYASF ,AGPEND
        INTEGER         IVPLIX  ,IVLTYP ,IVLWSC ,IVPLCI ,IVPMIX
        INTEGER         IVMTYP  ,IVMSZS ,IVPMCI ,IVTXIX ,IVTXP
        INTEGER         IVTXAL  ,IVCHH  ,IVCHOV ,IVTXFO ,IVTXPR
        INTEGER         IVCHXP  ,IVCHSP ,IVTXCI ,IVFAIX ,IVPASZ
        INTEGER         IVPARF  ,IVFAIS ,IVFASI ,IVFACI ,IVASF
        INTEGER         IP2AEA  ,IL2AEA
        INTEGER         IALTYP  ,IALWSC ,IAPLCI ,IAMTYP ,IAMSZS
        INTEGER         IAPMCI  ,IATXFP ,IACHXP ,IACHSP ,IATXCI
        INTEGER         IAFAIS  ,IAFASI ,IAFACI
        INTEGER         NCGASF  ,NGKASF ,MASMAP
C
        LOGICAL         ASFCHG(13)
        EQUIVALENCE     (VALCHG(25),ASFCHG(1))
C
C
        REAL UP, BASE, DMAX, FACTOR
C
        GOTO  (210, 220, 230, 240,
     +         250, 260, 270, 280,
     +         290, 300, 310, 320, 330, 340, 350, 360,
     +         370, 380, 390, 400, 410, 420, 430)       MCODES-20
C
C
C     GKS POLYLINE INDEX.
C
210     CONTINUE
        CALL GUPDVI (ID(1), IVPLIX, 1)
        RETURN
C
C     GKS LINETYPE.
C
220     CONTINUE
        CALL GUPDVI (ID, IVLTYP, 1)
        RETURN
C
C     GKS LINEWIDTH SCALE FACTOR.
C
230     CONTINUE
        CALL GUPDVR (RX, IVLWSC, 1)
        RETURN
C
C     GKS POLYLINE COLOR INDEX.
C
240     CONTINUE
        CALL GUPDVI (ID, IVPLCI, 1)
        RETURN
C
C     GKS POLYMARKER INDEX.
C
250     CONTINUE
        CALL GUPDVI (ID, IVPMIX, 2)
        RETURN
C
C     MARKER TYPE.
C
260     CONTINUE
        CALL GUPDVI (ID, IVMTYP, 2)
        RETURN
C
C     MARKER SIZE SCALE FACTOR.
C
270     CONTINUE
        CALL GUPDVR (RX, IVMSZS, 2)
        RETURN
C
C     GKS POLYMARKER COLOR INDEX.
C
280     CONTINUE
        CALL GUPDVI (ID, IVPMCI, 2)
        RETURN
C
C     TEXT INDEX.
C
290     CONTINUE
        CALL GUPDVI (ID, IVTXIX, 3)
        RETURN
C
C     TEXT FONT AND PRECISION.
C
300     CONTINUE
C
C       FONT.
C       CONVERT GKS FONT INDEX TO CGM POINTER TO FONT LIST.
C
        ID(1) = IABS(ID(1))
        CALL GUPDVI (ID(1), IVTXFO, 3)
C
C       PRECISION.
        CALL GUPDVI (ID(2), IVTXPR, 3)
        RETURN
C
C     CHARACTER EXPANSION FACTOR.
C
310     CONTINUE
        CALL GUPDVR (RX, IVCHXP, 3)
        RETURN
C
C     CHARACTER SPACING.
C
320     CONTINUE
        CALL GUPDVR (RX, IVCHSP, 3)
        RETURN
C
C     TEXT COLOR INDEX.
C
330     CONTINUE
        CALL GUPDVI (ID, IVTXCI, 3)
        RETURN
C
C     CHARACTER ORIENTATION VECTORS.
C
340     CONTINUE
C
C       EXTRACT AND PROCESS HEIGHT.
        UP   = SQRT (RX(1)**2+RY(1)**2)
        BASE = SQRT (RX(2)**2+RY(2)**2)
        DMAX = AMAX1(UP,BASE)
        IF (DMAX .GT. 1.0)  THEN
C
C           A VECTOR IS LONGER THAN 1.0 NDC,
C           SCALE DOWN BOTH VECTORS EQUALLY.
            FACTOR = 1.0/DMAX
            RX(1) = FACTOR*RX(1)
            RY(1) = FACTOR*RY(1)
            RX(2) = FACTOR*RX(2)
            RY(2) = FACTOR*RY(2)
        END IF
C
C       FIX HEIGHT AND PROCESS IT.
        RTMP = 0.5+UP*REAL(MYSCAL)
        ID(1) = MAXYVD
        IF (RTMP .LT. REAL(MAXYVD)) ID(1) = INT(RTMP)
        CALL GUPDVI (ID, IVCHH, 3)
C
C       CONVERT NDC VECTORS TO VDC AND PROCESS.
        ID(1) = MIN0 (MAXYVD, IFIX (0.5 + MXSCAL*RX(1)))
        ID(2) = MIN0 (MAXYVD, IFIX (0.5 + MYSCAL*RY(1)))
        ID(3) = MIN0 (MAXYVD, IFIX (0.5 + MXSCAL*RX(2)))
        ID(4) = MIN0 (MAXYVD, IFIX (0.5 + MYSCAL*RY(2)))
        CALL GUPDVI (ID, IVCHOV, 3)
        RETURN
C
C     TEXT PATH.
C
350     CONTINUE
        CALL GUPDVI (ID, IVTXP, 3)
        RETURN
C
C     TEXT ALIGNMENT.
C
360     CONTINUE
        CALL GUPDVI (ID, IVTXAL, 3)
        RETURN
C
C     GKS FILL AREA INDEX.
C
370     CONTINUE
        CALL GUPDVI (ID, IVFAIX, 4)
        RETURN
C
C     GKS FILL AREA INTERIOR STYLE.
C
380     CONTINUE
        CALL GUPDVI (ID, IVFAIS, 4)
        RETURN
C
C     GKS FILL AREA STYLE INDEX.
C
390     CONTINUE
        CALL GUPDVI (ID, IVFASI, 4)
        RETURN
C
C     GKS FILL AREA COLOR INDEX.
C
400     CONTINUE
        CALL GUPDVI (ID, IVFACI, 4)
        RETURN
C
C     GKS PATTERN SIZE.
C
410     CONTINUE
C
C       TRUNCATE DX,DY TO LIMITS OF NDC UNIT SQUARE,
C       CONVERT TO VDC, STORE AS HEIGHT AND WIDTH VECTORS.
        ID(4) = 0
        ID(3) = IFIX(FLOAT(MXSCAL)*(AMAX1(0.,AMIN1(1.0,RX(1)))))
        ID(2) = IFIX(FLOAT(MYSCAL)*(AMAX1(0.,AMIN1(1.0,RY(1)))))
        ID(1) = 0
        CALL GUPDVI (ID, IVPASZ, 4)
        RETURN
C
C     GKS PATTERN REFERENCE POINT.
C
420     CONTINUE
C
C       TRUNCATE X,Y TO LIMITS OF NDC UNIT SQUARE,
C       CONVERT TO VDC.
        ID(1) = IFIX(FLOAT(MXSCAL)*(AMAX1(0.,AMIN1(1.0,RX(1)))))
        ID(2) = IFIX(FLOAT(MYSCAL)*(AMAX1(0.,AMIN1(1.0,RX(2)))))
        CALL GUPDVI (ID, IVPARF, 4)
        RETURN
C
C     GKS ASPECT SOURCE FLAGS.
C
430     CONTINUE
        CALL GUPASF
        RETURN
        END
