C
C	$Id: gflcnv.f,v 1.1.1.1 1992-04-17 22:34:00 ncargd Exp $
C
      SUBROUTINE GFLCNV (FNUM,INUM)
C
C     THIS SUBROUTINE CONVERTS THE FLOATING-POINT NUMBER IN
C     FNUM INTO TWO 16-BIT INTEGERS IN THE LOW ORDER BITS OF
C     INUM.  THESE TWO 16-BIT QUANTITIES ARE SUITABLE FOR
C     INSERTION INTO THE CGM.  THE FIRST 16-BIT INTEGER IS
C     GIVEN BY THE LOW-ORDER 15 BITS RESULTING FROM INUM(1)=FUNM.
C     THIS NUMBER IS THEN EXPRESSED IN 2'S COMPLEMENT FORMAT.  THE
C     SECOND 16-BIT INTEGER IS THE DECIMAL PART OF FNUM
C     MULTIPLIED BY 2**16 (65536.)
C
      REAL     FNUM
      INTEGER  INUM(2)
C
      INTEGER  JNUM,JABS
C
C  Find the smallest integer less than or equal to FNUM and store
C  it in JNUM.
C
      JNUM = INT(FNUM)
      IF (FNUM.LT.0. .AND. REAL(JNUM).NE.FNUM) JNUM = JNUM-1
C
C  Find the fractional part.
C
      INUM(2) = 65536.*(FNUM-REAL(JNUM))
C
C  Define 2's complement of whole part.
C
      JABS = IABS(JNUM)
      INUM(1) = IAND(32767,JABS)
      IF (JNUM .LT. 0) THEN
        INUM(1) = (32767-INUM(1))+1
        INUM(1) = IOR(INUM(1),ISHIFT(1,15))
      ENDIF
C
      RETURN
      END
