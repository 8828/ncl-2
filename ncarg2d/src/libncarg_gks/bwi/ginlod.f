C
C	$Id: ginlod.f,v 1.1.1.1 1992-04-17 22:34:00 ncargd Exp $
C
      SUBROUTINE GINLOD(GKSERR)
C
C  THIS ROUTINE LOADS THE CURRENT INSTRUCTION INTO THE METAFILE
C       THE CURRENT INSTRUCTION INCLUDES THE OPCODE CLASS AND ID,
C       THE AND THE SIZE FILED WHICH MAY BE SHORT OR LONG.
C
C  OUTPUT
C       GKSERR-THE ERROR STATUS FLAG
C
C  ALL DATA IS TYPE INTEGER UNLESS OTHERWISE INDICATED
C
      IMPLICIT INTEGER (A-Z)
C
C  COMMON FOR COMMUNICATION OF INSTRUCTION AND LENGTH
C
      COMMON  /G01INS/  MCODES  ,MCONTS ,
     +                  MVDCFW  ,MCIXFW ,MDCCFW ,MIXFW  ,MINTFW ,
     +                  MDCCRG  ,MXOFF  ,MXSCAL ,MYOFF  ,MYSCAL ,
     +                  MINXVD  ,MAXXVD ,MINYVD ,MAXYVD ,
     +                  MCFRM   ,MCOPCL ,MCOPID ,MCNBYT ,
     +                  MCCBYT  ,MCFPP  ,MSLFMT ,MEFW   ,MCTCHG ,
     +                  MBCCHG
        INTEGER         MCODES  ,MCONTS
        INTEGER         MVDCFW  ,MCIXFW ,MDCCFW ,MIXFW  ,MINTFW
        INTEGER         MDCCRG  ,MXOFF  ,MXSCAL ,MYOFF  ,MYSCAL
        INTEGER         MINXVD  ,MAXXVD ,MINYVD ,MAXYVD
        INTEGER         MCFRM   ,MCOPCL ,MCOPID ,MCNBYT
        INTEGER         MCCBYT  ,MCFPP  ,MSLFMT ,MEFW   ,MCTCHG
        INTEGER         MBCCHG
      COMMON  /G01IO/   MIOFLG  ,MRECNM ,MPXYSZ ,MPXPY(256)     ,
     +                  MOBFSZ  ,MOUTBF(720)    ,MBFPOS ,
     +                  MFGLUN  ,MXBITS         ,MDTYPE ,
     +                  MNFFLG  ,MBMFLG ,MEMFLG
        INTEGER         MIOFLG  ,MRECNM ,MPXYSZ ,MPXPY  ,MOBFSZ ,
     +                  MBFPOS  ,MFGLUN ,MOUTBF ,MXBITS ,MDTYPE ,
     +                  MNFFLG  ,MBMFLG ,MEMFLG
      COMMON  /G01CHA/  MFNAME  ,MPNAME
      CHARACTER*80      MFNAME  ,MPNAME
C
C  DEFINE THE ALLOK STATUS AND THE OPCODE CLASS AND ID LENGTHS
C
      DATA ALLOK,OPCLLN,OPIDLN/0,4,7/
C
C  DEFINE THE SHORT FORMAT LENGTH, SHORT FORMAT COUNT, LONG FORMAT FLAG,
C    CONTINUE FLAG ON, CONTINUE FLAG OFF, CONTINUE LENGTH, LONG FORMAT
C    LENGTH
C
      DATA SHFMLN,SHTFMT,LFMFLG,CONON,CONOFF,CFMLNG,LFMLNG
     1          /5,30,31,1,0,1,15/
      DATA  SHORT/0/, LONG/1/
C
C  SET ERROR STATUS TO ALL OK
C
      GKSERR = ALLOK
C
C  MAKE SURE INSTRUCTION STARTS ON A 16 BIT BOUNDRY
C
      TEMP = MOD(MBFPOS,16)
      IF (TEMP .NE. 0) MBFPOS = MBFPOS + (16-TEMP)
C
C  LOAD THE OPCODE CLASS AND ID INTO THE METAFILE
C
      CALL GMFLOD(MCOPCL,OPCLLN,1,GKSERR)
      IF (GKSERR .NE. ALLOK) RETURN
      CALL GMFLOD(MCOPID,OPIDLN,1,GKSERR)
      IF (GKSERR .NE. ALLOK) RETURN
C
C  DETERMINE IF A LONG FORMAT OR SHORT FORMAT INSTRUCTION
C
      IF (MCCBYT .LE. SHTFMT) THEN
C
C       SHORT FORMAT INSTRUCTION
C
        MSLFMT = SHORT
        CALL GMFLOD (MCCBYT,SHFMLN,1,GKSERR)
        IF (GKSERR .NE. ALLOK) RETURN
      ELSE
C
C       LONG FORMAT INSTRUCTION
C       SET THE LONG FORMAT FLAG
C
        MSLFMT = LONG
        CALL GMFLOD (LFMFLG,SHFMLN,1,GKSERR)
        IF (GKSERR .NE. ALLOK) RETURN
C
C       SET THE CONTINUE FLAG
C
        IF (MCNBYT .NE. 0) THEN
C
C               THERE IS ANOTHER PARTITION
C
                CALL GMFLOD(CONON,CFMLNG,1,GKSERR)
        ELSE
C
C               LAST PARTITION
C
                CALL GMFLOD(CONOFF,CFMLNG,1,GKSERR)
        END IF
C
        IF (GKSERR .NE. ALLOK) RETURN
C
C       SET THE LONG FORMAT OPERAND LIST SIZE
C
        CALL GMFLOD(MCCBYT,LFMLNG,1,GKSERR)
      END IF
C
      RETURN
      END
