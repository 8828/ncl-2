C
C	$Id: gprec.f,v 1.1.1.1 1992-04-17 22:33:41 ncargd Exp $
C
      SUBROUTINE GPREC(IL,IA,RL,RA,SL,LSTR,STR,MLDR,ERRIND,LDR,DATREC)
      INTEGER EPREC
      PARAMETER (EPREC=107)
      INTEGER IL,IA(*),RL,SL,LSTR(*),MLDR,LDR,ERRIND
      REAL RA(*)
      CHARACTER*(*) STR(*)
      CHARACTER*80  DATREC(LDR)
      DATA NCE/1/
C
C     DETERMINE NUMBER OF CHARACTER*80 RECORDS THE PACKING IS GOING TO
C     REQUIRE:  THE INTEGERS ARE PACKED IN I20 FORMAT, THE REALS IN
C     E20.13 FORMAT, AND THE CHARACTERS ARE PACKED IN PACKETS OF
C     N PER RECORD, WHERE N IS 80 DIVIDED BY THE CHARACTER STRING
C     OF MAXIMUM LENGTH.
C
C---------------------------------------------------------------------
C
C     WHERE:
C       MAXSL = MAXIMUM LENGTH OF ALL CHARACTER STRINGS
C       NCE   = NUMBER OF CHARACTER ENTRIES IN EACH DATA RECORD
C       NTL   = NUMBER OF DATA RECORDS NEEDED TO STORE THE LSTR ARRAY
C       NIL   = NUMBER OF DATA RECORDS NEEDED TO STORE INTEGER ARRAY
C       NRL   = NUMBER OF DATA RECORDS NEEDED TO STORE THE REAL ARRAY
C       NSL   = NUMBER OF DATA RECORDS NEEDED TO STORE THE CHARACTERS
C
C     THE PACKED DATA RECORD STRUCTURE LOOKS LIKE:
C
C     DATA REC NUMBER           STORED ITEMS
C     ---------------           ------------
C     1                         IL,RL,SL,NTL,NIL,NRL,NSL,MAXSL
C                               (STORED AS 8I10)
C     2 TO NTL+1                LSTR ARRAY STORED AS I10 INTEGERS
C     NTL+2 TO NTL+NIL+1        INTEGER ARRAY STORED AS I20
C     NTL+NIL+2 TO              REAL ARRAY STORED AS E20.13
C       NTL+NIL+NRL+1
C     NTL+NIL+NRL+2 TO          CHARACTERS STORED IN BLOCKS OF NCE
C       NTL+NIL+NRL+NSL+1
C
      NIL = 0
      IF (IL.GT.0) THEN
      NIL = IL/4
      IF (MOD(IL,4).NE.0) THEN
      NIL = NIL+1
      ENDIF
      ENDIF
      NRL = 0
      IF (RL.GT.0) THEN
      NRL = RL/4
      IF (MOD(RL,4).NE.0) THEN
      NRL = NRL+1
      ENDIF
      ENDIF
C
C     FIND MAXIMUM LENGTH OF THE CHARACTER STRINGS
C
      MAXSL = 0
      IF (SL.GT.0) THEN
      DO   200 I=1,SL
      MAXSL = MAX0(MAXSL,LSTR(I))
  200 CONTINUE
      ENDIF
C
C     DETERMINE NUMBER OF CHARACTER ENTRIES TO BE PACKED IN EACH
C     DATA RECORD
C
      IF (MAXSL.NE.0) THEN
      NCE = 80/MAXSL
      IF (NCE.LE.1) THEN
      NCE = 1
      ENDIF
      ENDIF
C
C     DETERMINE THE NUMBER OF DATA RECORDS NEEDED TO STORE THE LSTR
C     ARRAY (STORED 8I10)
C
      NTL = 0
      IF (SL.GT.0) THEN
      NPER = 8
      NTL = SL/NPER
      IF (MOD(SL,NPER).NE.0.OR.NTL.EQ.0) THEN
      NTL = NTL+1
      ENDIF
      ENDIF
C
C     DETERMINE NUMBER OF RECORDS NEEDED TO STORE THE CHARACTERS
C
      NSL = 0
      IF (SL.GT.0) THEN
      IF (NCE.GT.1) THEN
      NSL = SL/NCE
      IF (MOD(SL,NCE).NE.0.OR.NSL.EQ.0) THEN
      NSL = NSL+1
      ENDIF
      ELSE
      DO   201 I=1,SL
      K = LSTR(I)/80
      IF (MOD(LSTR(I),80).NE.0.OR.K.EQ.0) THEN
      NSL = NSL+K+1
      ELSE
      NSL = NSL+K
      ENDIF
  201 CONTINUE
      ENDIF
      ENDIF
      LDR = NTL+NIL+NRL+NSL+1
      IF (LDR.GT.MLDR) THEN
      ERRIND = 2001
      RETURN
      ENDIF
C
C     INITIALIZE THE DATA RECORD
C
      DO   202 I=1,LDR
      DATREC(I) = ' '
  202 CONTINUE
C
C     PACK THE DATA RECORD
C
      WRITE(DATREC(1),501) IL,RL,SL,NTL,NIL,NRL,NSL,MAXSL
C
C     WRITE OUT LSTR ARRAY
C
      NPER = 8
      IF (SL.GT.0) THEN
      INDX1 = SL/NPER
      INDX2 = MOD(SL,NPER)
      IF (INDX1.EQ.0) THEN
      WRITE(DATREC(2),501) (LSTR(LL),LL=1,SL)
      ELSE
      DO   203 I=1,INDX1
      IPNT1 = NPER*(I-1)+1
      IPNT2 = NPER*I
      WRITE(DATREC(I+1),501) (LSTR(LL),LL=IPNT1,IPNT2)
  203 CONTINUE
      IF (INDX2.NE.0) THEN
      NPNT1 = NPER*INDX1+1
      NPNT2 = NPER*INDX1+INDX2
      WRITE(DATREC(INDX1+2),501) (LSTR(LL),LL=NPNT1,NPNT2)
      ENDIF
      ENDIF
      ENDIF
C
C     WRITE OUT INTEGER ARRAY
C
      NPER = 4
      IF (IL.GT.0) THEN
      INDX1 = IL/NPER
      INDX2 = MOD(IL,NPER)
      IF (INDX1.EQ.0) THEN
      WRITE(DATREC(NTL+2),502) (IA(LL),LL=1,IL)
      ELSE
      DO   204 I=1,INDX1
      IPNT1 = NPER*(I-1)+1
      IPNT2 = NPER*I
      WRITE(DATREC(NTL+I+1),502) (IA(LL),LL=IPNT1,IPNT2)
  204 CONTINUE
      IF (INDX2.NE.0) THEN
      NPNT1 = NPER*INDX1+1
      NPNT2 = NPER*INDX1+INDX2
      WRITE(DATREC(NTL+INDX1+2),502) (IA(LL),LL=NPNT1,NPNT2)
      ENDIF
      ENDIF
      ENDIF
C
C     WRITE OUT REAL ARRAY
C
      NPER = 4
      IF (RL.GT.0) THEN
      INDX1 = RL/NPER
      INDX2 = MOD(RL,NPER)
      IF (INDX1.EQ.0) THEN
      WRITE(DATREC(NTL+NIL+2),503) (RA(LL),LL=1,RL)
      ELSE
      DO   205 I=1,INDX1
      IPNT1 = NPER*(I-1)+1
      IPNT2 = NPER*I
      WRITE(DATREC(NTL+NIL+I+1),503) (RA(LL),LL=IPNT1,IPNT2)
  205 CONTINUE
      IF (INDX2.NE.0) THEN
      NPNT1 = NPER*INDX1+1
      NPNT2 = NPER*INDX1+INDX2
      WRITE(DATREC(NTL+NIL+INDX1+2),503) (RA(LL),LL=NPNT1,NPNT2)
      ENDIF
      ENDIF
      ENDIF
C
C     WRITE OUT CHARACTER ARRAYS
C
      NPER = NCE
      IF (SL.GT.0) THEN
      IF (NPER.GT.1) THEN
      INDX1 = SL/NPER
      INDX2 = MOD(SL,NPER)
      IF (INDX1.EQ.0) THEN
      DO   206 I=1,SL
      IPNT1 = (I-1)*MAXSL+1
      IPNT2 = IPNT1+LSTR(I)-1
      DATREC(NTL+NIL+NRL+2)(IPNT1:IPNT2) = STR(I)
  206 CONTINUE
      ELSE
      DO   207 I=1,INDX1
      DO   208 J=1,NCE
      JPNT1 = (J-1)*MAXSL+1
      JPNT2 = JPNT1+LSTR(NPER*(I-1)+J)-1
      DATREC(NTL+NIL+NRL+I+1)(JPNT1:JPNT2) = STR(NPER*(I-1)+J)
  208 CONTINUE
  207 CONTINUE
      IF (INDX2.NE.0) THEN
      DO   209 J=1,INDX2
      JPNT1 = (J-1)*MAXSL+1
      JPNT2 = JPNT1+LSTR(NPER*INDX1+J)-1
      DATREC(NTL+NIL+NRL+INDX1+2)(JPNT1:JPNT2) = STR(NPER*INDX1+J)
  209 CONTINUE
      ENDIF
      ENDIF
      ELSE
      JNDX = 0
      DO   210 I=1,SL
      INDX1 = LSTR(I)/80
      INDX2 = MOD(LSTR(I),80)
      IF (INDX1.EQ.0) THEN
      JNDX = JNDX+1
      DATREC(NTL+NIL+NRL+JNDX+1) = STR(I)(1:INDX2)
      ELSE
      DO   211 J=1,INDX1
      JNDX = JNDX+1
      DATREC(NTL+NIL+NRL+JNDX+1) = STR(I) (80*(J-1)+1:80*J)
  211 CONTINUE
      IF (INDX2.NE.0) THEN
      JNDX = JNDX+1
      DATREC(NTL+NIL+NRL+JNDX+1) = STR(I) (80*INDX1+1:80*INDX1+INDX2)
      ENDIF
      ENDIF
  210 CONTINUE
      ENDIF
      ENDIF
C
  501 FORMAT(8I10)
  502 FORMAT(4I20)
  503 FORMAT(4E20.13)
C
      RETURN
      END
