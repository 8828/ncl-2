#!/bin/csh -f
#
#   $Id: create_src_dist,v 1.6 2009-01-16 18:51:25 haley Exp $
#
#######################################################################
#                                                                     #
#              Copyright (C)  2007                                    #
#        University Corporation for Atmospheric Research              #
#              All Rights Reserved                                    #
#                                                                     #
#######################################################################
#
#   File:       create_src_dist
#
#   Author:     Mary Haley
#           National Center for Atmospheric Research
#           PO 3000, Boulder, Colorado
#
#   
#   Date:       Tue Mar 27 09:27:53 MDT 2007
#
#   Description:  Tars up NCL and/or NCAR Graphics source for release to users.
#
# There are two types of releases possible:
#
#      - NCL and NCAR Graphics source code (default)
#      - NCAR Graphics source code
#
#   Options:   -r :  root directory
#              -1 :  tar up just NCAR Graphics (not NCL)
#              -i :  Tar up files for internal use. Includes "install"
#                    directory
#              -c :  scp to windom.ucar.edu
#              -t :  Include triangle code
#              -v :  version number
#              -x :  don't use CVS to get directory. Assume "ncarg"
#
# Set defaults for the root directory, the tar file directory, and the
# version number.
#
# set echo verbose timestamp
#
set rootdir     = "$NCARG"
set parentdir   = `pwd`
set scpmach     = "windom.ucar.edu"
set version     = ""
set cvsroot     = "windom.ucar.edu:/fs/scd/home1/ncargd/CVS"
set cvsrsh      = "ssh"
set tardir1     = "/fs/scd/home1/ncargd/dist/tarfiles"
set tardir2     = "/fis/scd/home/ncargd"
set extra       = ""
set extra2      = "wo_tri"      # Without Shewchuk Triangle code

if (-e $tardir1) then
  set tardir = $tardir1
else if (-e $tardir2) then 
  set tardir = $tardir2
else
  set tardir = `pwd`
endif

#
# Sort through options.
#

unset SCP
set USE_CVS
unset INC_TRI

set NCARGNCL

while ($#argv > 0)
  switch ($1)
    case "-1":
      set NCARGONLY
      unset NCARGNCL
      shift
      breaksw

    case "-i":
      set INTERNAL
      unset NCARGNCL
      shift
      breaksw

    case "-v":
      shift
      set version = "$1"
      shift
      breaksw

    case "-d":
      shift
      set tardir = "$1"
      shift
      breaksw

    case "-t":
      set INC_TRI
      set extra2 = "w_tri"      # With Shewchuk Triangle code
      shift
      breaksw

    case "-c":
      set SCP
      shift
      breaksw

    case "-e":
      shift
      set extra = "$1"
      shift
      breaksw

    case "-r":
      shift
      set rootdir = "$1"
      shift
      breaksw

    case "-p":
      shift
      set parentdir = "$1"
      shift
      breaksw

    case "-x":
      unset USE_CVS
      shift
      breaksw

    case "-h":
    case "-help":
      goto usage
      breaksw

    default:
      echo "option not recognized"
      exit 1
    endsw
end

#
# Error checking.
#
if (! -e $tardir) then
  echo "Directory $tardir does not exist. Please choose another directory."
  exit 1
endif

# Set directory to scp source files to.

set scpdir = "/fs/scd/home1/ncargd/dist/tarfiles/ncl/$version"

#
# CVS check out the latest source code.
#
# First set some CVS environment variables.
#
setenv CVSROOT $cvsroot
setenv CVS_RSH $cvsrsh


echo ""
if ($?USE_CVS) then
  if ($?INC_TRI) then
    echo "  I will be CVS checking out the source code in the directory"
    echo "  '$parentdir' and including the triangle code."
  else
    echo "  I will be CVS checking out the source code in the directory"
    echo "  '$parentdir' and NOT including the triangle code."
  endif
else
  echo "  I will be trying to use the 'ncarg' directory in"
  echo "  '$parentdir'. Continue? (y/n) "
endif
echo ""
echo -n "Continue? (y/n) "

set answer="$<"
if ("$answer" != "y") then
  exit
endif

#
# What if there's already an "ncarg" directory?
#
cd $parentdir
if ($?USE_CVS && -e ncarg) then
  echo "Directory $parentdir/ncarg already exists." 
  echo -n "Do you want me to remove it? (y/n) "
  set answer="$<"
  if ("$answer" != "y") then
    echo "Directory '$parentdir/ncarg' already exists and may not be"
    echo "the correct source code. Use the '-x' option if it's okay to"
    echo "use this directory."
    exit
  endif
  set cmd = "/bin/rm -rf $parentdir/ncarg"
  echo "$cmd"
  $cmd
endif

# 
# Check out all of $CVSROOT/ncarg
#
if ($?USE_CVS) then
  set cmd = "cvs co ncarg"
  echo "$cmd"
  $cmd
  if (! -e ncarg) then
    echo "Directory '$parentdir/ncarg' does not exist. The CVS command might"
    echo "have failed. Can't continue."
    exit
  endif
else
  if (! -e ncarg) then
    echo "Directory '$parentdir/ncarg' does not exist. You either need" 
    echo "to let this script try to check it out with CVS, or check it"
    echo "out yourself. Don't use the '-x' option if you want this script
    echo "to check it out for you."
    exit
  endif
endif


# 
# Remove "CVS" directories
#
cd $parentdir/ncarg
set cmd = "find . -name 'CVS' -type d -exec /bin/rm -rf {} \;"
echo "$cmd"
find . -name 'CVS' -type d -exec /bin/rm -rf {} \;
cd $parentdir

#
# Get the version, if not already set by user.
#
if ($version == "") then
  if ($?NCARGONLY) then
    set version = `cat ncarg/NcargVersion`
  else
    set version = `cat ncarg/NclVersion`
  endif
endif

if ($?NCARGONLY) then
  set src_prefix = "ncarg-$version"
  set tar_prefix = "ncarg_src"
else if ($?NCARGNCL) then
  set src_prefix = "ncl_ncarg-$version"
  set tar_prefix = "ncl_ncarg_src"
else
  set src_prefix = "ncl_ncarg_internal-$version"
  set tar_prefix = "ncl_ncarg_internal_src"
endif
set src_tar_dir = "$parentdir/$src_prefix"
 
#
# What if there's already a $src_prefix directory?
#
cd $parentdir
if (-e $src_prefix) then
  echo "Directory $parentdir/$src_prefix already exists." 
  echo "I have to remove it, otherwise this script won't work."
  echo -n "Do you want me to remove it? (y/n) "
  set answer="$<"
  if ("$answer" != "y") then
    echo "I can't continue, since $src_prefix exists."
    exit
  endif
  set cmd = "/bin/rm -rf $parentdir/$src_prefix"
  echo "$cmd"
  $cmd
endif

if ($extra != "") then
  set tar_prefix = "{$tar_prefix}_{$extra2}_{$extra}"
else 
  set tar_prefix = "{$tar_prefix}_{$extra2}"
endif

set tar_filename = "$tar_prefix-$version.tar"

# 
# Set up directories and files to remove (that is, that we don't
# want to give to the user).
#

set ni_stuff = "ni/src/ngi ni/src/lib/xcb ni/src/lib/ngo"
set ext_stuff = "external/fftpack5 external/sphere3.1 external/sphere3_dp"
set tri_stuff = "ni/src/lib/hlu/triangle.c ni/src/lib/hlu/triangle.h"
set stuff_to_always_remove = "pynio"

if ($?NCARGONLY) then       # NCAR Graphics only, for users.
  set more_stuff_to_remove = "install ni external"
else if ($?NCARGNCL) then   # NCL and NCAR Graphics, for users.
  if ($?INC_TRI) then
    set more_stuff_to_remove = "install $ext_stuff $ni_stuff"
  else 
    set more_stuff_to_remove = "install $ext_stuff $ni_stuff $tri_stuff"
  end if
else  # NCL and NCAR Graphics, for internal use, not for outside users
  set more_stuff_to_remove = "$ext_stuff $ni_stuff"
endif

set stuff_to_remove = "$stuff_to_always_remove $more_stuff_to_remove"

foreach f($stuff_to_remove)
  set cmd = "/bin/rm -rf $parentdir/ncarg/$f"
  echo "$cmd"
  $cmd
end

#
# Rename the parent "ncarg" directory to something that indicates
# what kind of source we have, and what version.
#
mv ncarg $src_prefix

echo -n "The tar file will be called $tar_filename and it will be put in $tardir. Continue? (y/n) "
set answer="$<"
if ("$answer" != "y") then
  exit
endif

#
# Tar up source directory
#
echo "Tarring up the files..."
tar -chf $tardir/$tar_filename $src_prefix

#
# Compress file.
#
echo "Gzipping the tar file..."
gzip $tardir/$tar_filename
if (-f $tardir/$tar_filename.gz) then 
  echo "Created '$tardir/$tar_filename.gz'..."
else
  echo "Error, no compressed tar file created."
  exit
endif

if ($?SCP) then
  echo "scp (via windom) or move file to $scpdir..."
  if ("$name" == "meeker" || "$name" == "windom" || "$name" == "longs") then
    /bin/mv $tardir/$tar_filename.gz $scpdir
  else
    scp $tardir/$tar_filename.gz $scpmach\:$scpdir
  endif
endif

exit 0

usage:
echo "usage: create_src_dist [options]"
echo ""
echo "   Options:   -r : root directory"
echo "              -d : tar file directory"
echo "              -c : scp to windom.ucar.edu"
echo "              -t : include triangle code in HLU directory"
echo "              -v : version number"
echo "              -e : extra string to include in tarfile name"
echo "              -1 : tar up NCARG source files only"
echo "              -i : tar up source for internal use"
echo ""
exit
